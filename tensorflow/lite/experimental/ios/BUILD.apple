# TensorFlow Lite for iOS

load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("//tensorflow/lite/experimental/ios:ios.bzl", "TFL_MINIMUM_OS_VERSION")
load("@build_bazel_rules_apple//apple:ios.bzl", "ios_static_framework")

package(
    default_visibility = [
        "//tensorflow/lite:__subpackages__",
    ],
    licenses = ["notice"],  # Apache 2.0
)

genrule(
    name = "strip_coreml_include_hdr",
    srcs = ["//tensorflow/lite/experimental/delegates/coreml:coreml_delegate.h"],
    outs = ["coreml_delegate.h"],
    cmd = """
    sed 's/#include \".*common.h"/#include \"common.h\"/' \
    "$(location //tensorflow/lite/experimental/delegates/coreml:coreml_delegate.h)" \
    > "$@"
    """,
)

TFL_FRAMEWORK_HDRS = [
    "//tensorflow/lite/delegates/gpu:metal_delegate.h",
    "//tensorflow/lite/c:c_api.h",
    "//tensorflow/lite/c:common.h",
]

# bazel build -c opt --config=ios_fat //tensorflow/lite/experimental/ios:TensorFlowLiteC_framework
ios_static_framework(
    name = "TensorFlowLiteC_framework",
    hdrs = TFL_FRAMEWORK_HDRS,
    bundle_name = "TensorFlowLiteC",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        ":tensorflow_lite_c",
    ],
)

# This target builds the flex delegate as a separate static framework, which
# does not include the TensorFlow Lite runtime. As this target does not contain
# TensorFlow Lite runtime, it is intended to be linked along with the
# TensorFlowLiteC framework above in a composable way.
#
# The flex delegate cannot be built for i386, so it can't be built with ios_fat
# config.
#
# bazel build -c opt --config=ios --ios_multi_cpus=armv7,arm64,x86_64 //tensorflow/lite/experimental/ios:TensorFlowLiteSelectTfOps_framework
ios_static_framework(
    name = "TensorFlowLiteSelectTfOps_framework",
    bundle_name = "TensorFlowLiteSelectTfOps",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        "//tensorflow/lite/delegates/flex:delegate",
    ],
)

# This target builds the Core ML delegate as a separate static framework, which
# does not include the TensorFlow Lite runtime. As this target does not contain
# TensorFlow Lite runtime, it is intended to be linked along with the
# TensorFlowLiteC framework above in a composable way.
#
# bazel build -c opt --config=ios_fat //tensorflow/lite/experimental/ios:TensorFlowLiteCCoreMl_framework
ios_static_framework(
    name = "TensorFlowLiteCCoreML_framework",
    hdrs = [
        ":coreml_delegate.h",
    ],
    avoid_deps = [
        ":tensorflow_lite_c",
    ],
    bundle_name = "TensorFlowLiteCCoreML",
    minimum_os_version = TFL_MINIMUM_OS_VERSION,
    deps = [
        "//tensorflow/lite/experimental/delegates/coreml:coreml_delegate",
    ],
)

cc_library(
    name = "tensorflow_lite_c",
    hdrs = [
        "//tensorflow/lite/c:c_api.h",
        "//tensorflow/lite/c:common.h",
        "//tensorflow/lite/delegates/gpu:metal_delegate.h",
    ],
    linkopts = [
        "-Wl,-weak_framework,Metal",
    ],
    tags = [
        "nobuilder",
        "swift_module=TensorFlowLiteC",
    ],
    deps = [
        "//tensorflow/lite/c:c_api",
        "//tensorflow/lite/delegates/gpu:metal_delegate",
    ],
)

# Used for building TensorFlowLiteC framework.
build_test(
    name = "framework_build_test",
    tags = [
        "noasan",  # b/147230742
        "nomsan",  # b/145205324
        "notsan",  # b/145205324
    ],
    targets = [
        ":TensorFlowLiteC_framework",
        ":TensorFlowLiteSelectTfOps_framework",
    ],
)
